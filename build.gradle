buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.3.1'
	}
}
allprojects {
	repositories {
		jcenter()
	}
}

apply plugin: 'com.android.application'

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.1"

	defaultConfig {
		applicationId "com.samwhited.opensharelocationplugin"
		minSdkVersion 17
		targetSdkVersion 23
		versionCode 6
		versionName "2.0.0"
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	//
	// To sign release builds, create the file `gradle.properties` in
	// $HOME/.gradle or in your project directory with this content:
	//
	// mStoreFile=/path/to/key.store
	// mStorePassword=xxx
	// mKeyAlias=alias
	// mKeyPassword=xxx
	//
	if (project.hasProperty('mStoreFile') &&
			project.hasProperty('mStorePassword') &&
			project.hasProperty('mKeyAlias') &&
			project.hasProperty('mKeyPassword')) {
		signingConfigs {
			release {
				storeFile file(mStoreFile)
				storePassword mStorePassword
				keyAlias mKeyAlias
				keyPassword mKeyPassword
			}
		}
		buildTypes.release.signingConfig = signingConfigs.release
		buildTypes.debug.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
		buildTypes.debug.signingConfig = null
	}

	applicationVariants.all { variant ->
		variant.outputs.each { output ->
			def SIGNED = variant.signingConfig != null ? 'signed' : 'unsigned'
			def alignedOutputFile = output.outputFile
			if (variant.buildType.zipAlignEnabled) {
				output.outputFile = new File(
						(String)alignedOutputFile.parent,
						rootProject.name + "-${variant.name}-${SIGNED}-${variant.versionName}.apk"
				)
			}
		}
	}
}

dependencies {
	compile 'com.android.support:appcompat-v7:23.0.1'
	compile 'org.osmdroid:osmdroid-android:4.3'
	compile 'org.slf4j:slf4j-simple:1.7.9'
	compile 'com.android.support:support-v4:23.0.1'
}
