buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}
allprojects {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.samwhited.opensharelocationplugin"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 15
        versionName "2.4.0"
        manifestPlaceholders = [apiKey:thunderforestAPIKey]
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    //
    // To sign release builds, create the file `gradle.properties` in
    // $HOME/.gradle or in your project directory with this content:
    //
    // mStoreFile=/path/to/key.store
    // mStorePassword=xxx
    // mKeyAlias=alias
    // mKeyPassword=xxx
    //
    if (project.hasProperty('mStoreFile') &&
            project.hasProperty('mStorePassword') &&
            project.hasProperty('mKeyAlias') &&
            project.hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(project.properties.get('mStoreFile'))
                storePassword project.properties.get('mStorePassword')
                keyAlias project.properties.get('mKeyAlias')
                keyPassword project.properties.get('mKeyPassword')
            }
        }
        defaultConfig.signingConfig = signingConfigs.release
        buildTypes.release.signingConfig = signingConfigs.release
        buildTypes.debug.signingConfig = signingConfigs.release
    }
}

task copyAssets(type: Copy)
copyAssets {
    description = 'Copies asset files into the appropriate source directory'
    from '.'
    into 'src/main/assets/'
    include('LICENSE')
}

preBuild.dependsOn(copyAssets)

apply plugin: 'base'
clean.doFirst({
    delete('src/main/assets')
})

ext {
    supportLibVersion = '27.1.0'
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation 'org.osmdroid:osmdroid-android:6.0.1'
}
